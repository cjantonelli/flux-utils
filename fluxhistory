#!/usr/bin/perl -wT
#
#  fluxhistory
#
#  Retrieves historical job information from Elasticsearch.  Usage:
#
#  fluxhistory [--days=N] [--user=NAME] [--account=NAME]
#
#  If not specified, days defaults to 1.  Legal values for days ranges
#  from 0 to 59.
#

use strict;
use warnings;


BEGIN {
    push( @INC, '/usr/cac/rhel6/lsa/flux-utils/perl5/lib64/perl5' );
    push( @INC, '/usr/cac/rhel6/lsa/flux-utils/perl5/share/perl5' );
    %ENV = (); # for security
}

use Search::Elasticsearch;
use Getopt::Long;
use Date::Format;
use Data::Dumper;

my $opt_days    = 1;
my $opt_help    = 0;
my $opt_account = '';
my $opt_user    = '';

my $user = getpwuid( $< ) || $ENV{'USER'} || '';


sub getjobinfo {
    my $entry = $_[0];
    my $item  = $_[1];
    return defined( $entry->{$item} ) ?  $entry->{$item} : '';
}


my $result = GetOptions(
  'days|d=i' => \$opt_days,
  'user|u=s' => \$opt_user,
  'account|a=s' => \$opt_account,
  'help'   => \$opt_help,
  );
if ( ! $result || $opt_help || scalar( @ARGV ) != 0 ) {
    print STDERR "Shows information about jobs that have ended in the past N days.\n\n";
    print STDERR "usage: \n";
    print STDERR "  $0 [--days=N] [--user=NAME] [--account=NAME]\n\n";
    print STDERR "  optional arguments:\n";
    print STDERR "    -u, --user=NAME     show jobs for user NAME\n";
    print STDERR "    -a, --account=NAME  show jobs for flux account NAME\n";
    print STDERR "    -d, --days=N        show jobs ending in the last N days(default: 1)\n";
    print STDERR "    -h, --help          show this help message and exit\n";
    exit( $opt_help ? 0 : 1 );
}

if ( $opt_days < 0 || $opt_days > 59 ) {
    print STDERR "$0: days must be in the range 0..59\n";
    exit( 1 );
}

if ( $opt_user ) {
    $user = $opt_user;
}
if ( $user !~ /^[a-z0-9]{1,16}$/ ) {
    print STDERR "$0: illegal username: ${user}\n";
    exit( 1 );
}

if ( $opt_account && $opt_account !~ /^[a-z0-9_]{1,32}$/ ) {
    print STDERR "$0: illegal account: ${opt_account}\n";
    exit( 1 );
}

my $d = $opt_days + 1;
my $t = time();
my ($sec,$min,$hour,$mday,$mon,$year,$wday,$yday,$isdst) = localtime( $t );
my $indices = sprintf( 'logstash-joblogs-%04d.%02d.%02d', $year + 1900,
    $mon + 1, $mday );
$d--;
while ( $d > 0 ) {
    $t -= 86400;
    ($sec,$min,$hour,$mday,$mon,$year,$wday,$yday,$isdst) = localtime( $t );
    $indices = sprintf( 'logstash-joblogs-%04d.%02d.%02d,', $year + 1900,
        $mon + 1, $mday ) . $indices;
    $d--;
}

my $e = Search::Elasticsearch->new( nodes => 'flux-es.engin.umich.edu:9200' );

my $query;
if ( $opt_account ) {
    $query = "type:pbsacctlog AND queue_state:E AND account:${opt_account}";
} else {
    $query = "type:pbsacctlog AND queue_state:E AND user:${user}";
}

my $results = $e->search(
    index => $indices,
    body  => {
        'size'  => 10000,
        'sort'  => [ { '@timestamp' => { 'order' => 'asc' } } ],
        'query' => { 'query_string' => { 'query' => $query } },
    }
);
#print Dumper($results) . "\n";

print "      Job Name           User     Account         Start time          Exit  Runtime   Memory used\n";
print "--------- -------------- -------- --------------- ------------------- ---- --------- ------------\n";
#print " 14973284 lsa_flux_check markmont support_flux    2015-03-04 22:06:31    0  00:00:42      26356kb\n";

my $logs = $results->{'hits'}->{'hits'};
for my $log (@$logs) {
    if ( defined( $log->{'_source'} ) ) {
        my $entry = $log->{'_source'};
        my $output = sprintf( "%9s %-14s %-8s %-15s %-19s %4d %9s %12s\n",
            getjobinfo( $entry, 'jobid' ),
            getjobinfo( $entry, 'jobname' ),
            getjobinfo( $entry, 'user' ),
            getjobinfo( $entry, 'account' ),
            time2str( "%Y-%m-%d %T", getjobinfo( $entry, 'start' ) ),
            getjobinfo( $entry, 'Exit_status' ),
            getjobinfo( $entry, 'resources_used.walltime' ),
            getjobinfo( $entry, 'resources_used.mem' ),
            );
        print $output;
    } else {
            print 'NO ENTRY: ' . Dumper( $log ) . "\n";
    }
}
#print Dumper($logs) . "\n";

